plugins {
  id 'nebula.provided-base' version '2.2.2'
}

task createWrapper(type: Wrapper) {
  description = "Create a gradle wrapper for you project"
  gradleVersion = '2.4'
}

allprojects {

  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'signing'
  apply plugin: 'idea'
  apply plugin: 'eclipse'

  // apply nebula plugins to all projects
  apply plugin: "nebula.provided-base"

  // code checks
  apply plugin: 'findbugs'
  apply plugin: 'pmd'
  apply plugin: 'checkstyle'
  apply plugin: "jacoco"

  group = "com.capitalone"
  version = "1.0.0"
  ext.vendor = "capitalone"

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  ext.springVer = "4.1.0.RELEASE"
  ext.springBootVer = "1.2.5.RELEASE"

  repositories {
    mavenCentral()
  }


  /// Code checks
  checkstyle {
    configFile = rootProject.file('gradle/config/checkstyle.xml')
    ignoreFailures = true // for now..
  }
  pmd {
    ignoreFailures = true // for now..
  }
  findbugs {
    ignoreFailures = true // for now..
  }
  // exclude the checkstyle, findbugsTest, pmdTest on test source..
  gradle.startParameter.excludedTaskNames += ["checkstyleTest", "findbugsTest", "pmdTest"]
  jacoco {
    toolVersion = '0.7.2.201409121644'
  }
  tasks.check.dependsOn(jacocoTestReport)
  ////

  task sourcesJar(type: Jar) {
      from sourceSets.main.allJava
  }

  task docJar(type: Jar, dependsOn: 'javadoc') {
      from javadoc.destinationDir
  }

  artifacts { // define the artifacts for a service
      archives sourcesJar { classifier "sources" }
      archives docJar { classifier "javadoc" }
  }

  jar.doFirst {
    manifest {
        attributes 'Implementation-Title': "${archivesBaseName}",
                   'Implementation-Version': "${version}",
                   'Implementation-Vendor': "${vendor}",
                    provider: 'gradle'
    }
  }
}

def sonatypeRepositoryUrl
def isRelease = false
//set build variables based on build type (release, continuous integration, development)
if (hasProperty("release")) {
  sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
  isRelease = true
} else if (hasProperty("snapshot")) {
  version += "-SNAPSHOT"
  isRelease = true
} else {
  version += "-SNAPSHOT"
  sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
  ext.ossrhUsername = ""
  ext.ossrhPassword = ""
}

signing {
  if (isRelease) {
    sign configurations.archives
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: sonatypeRepositoryUrl) {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }
      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'hygieia'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'Hygieia is a single, configurable, easy to use dashboard to visualize near real-time status of the entire delivery pipeline.'
        url 'https://github.com/capitalone/Hygieia'

        scm {
          connection 'scm:git:git://github.com/capitalone/Hygieia.git'
          developerConnection 'scm:git:git@github.com:capitalone/Hygieia.git'
          url 'https://github.com/capitalone/Hygieia'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'hygieia'
            url 'https://github.com/Hygieia'
            name 'Capital One'
            email 'Hygieia@capitalone.com'
          }
        }
      }
    }
  }
}



// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
}

archivesBaseName = "hygieia"

dependencies {
  testCompile "junit:junit:4.11"
}
